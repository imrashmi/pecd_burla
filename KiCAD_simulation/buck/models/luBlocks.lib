* LIBRARY OF MY CUSTOM BLOCKS
* Department: DESE, Indian Institute of Science, Bangalore

***************************************************************
* PWM
***************************************************************
.SUBCKT PWM_1ph nPin nPout fs=10000
atri nPin nPtri triout
.model triout triangle(cntl_array=[0 1] freq_array=[{fs} {fs}] out_low = 0 out_high = 1.0 duty_cycle = 0.5)
acompare [nPin nPtri] ncout comp_sum
.model comp_sum summer(in_gain=[1.0 -1.0])
alim ncout nPout limit_comp
.model limit_comp limit(gain=1e6 out_lower_limit=0.0 out_upper_limit=1.0 fraction=FALSE)
.ENDS
***************************************************************
*-------------------------------------------------
.SUBCKT PWMBridge nPin nArm1T nArm1B nArm2T nArm2B fs=10000
atri1 nPin nPtri1 triout1
.model triout1 triangle(cntl_array=[0 1] freq_array=[{fs} {fs}] out_low = -1.0 out_high = 1.0 duty_cycle = 0.5)
ainv nPin nPinv amp
.model amp gain(gain=-1.0)
acompare1 [nPin nPtri1] ncout1 comp_sum
acompare2 [nPtri1 nPinv] ncout2 comp_sum
.model comp_sum summer(in_gain=[1.0 -1.0])
alim1 ncout1 nArm1T limit_comp
alim2 ncout2 nArm1B limit_comp
alim3 ncout2 nArm2T limit_comp
alim4 ncout1 nArm2B limit_comp
.model limit_comp limit(gain=1e6 out_lower_limit=0.0 out_upper_limit=1.0 fraction=FALSE)
.ENDS
*$
*------------------------------------------------------
*-----------------------------------------------------------------
* OP AMPs
*-----------------------------------------------------------------
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.SUBCKT OPAMP    1 2 3 4 5
*opamp is considered as a compare, with pi controller
Bvref nref 0 V=v(1)
Bvfb nfb 0 V=v(2)
Bverr nerr 0 V=v(nref)-v(nfb)
Bic 0 nic i=v(nerr)
Ccontrol nic 0 1uF
Bir 0 nir i=v(nerr)
Rcontrol nir 0 1e6
Vigain nig 0 10
BVc nc 0 V=v(nic)*v(nig)+v(nir)
Blimit 5 0 V=v(nc) < v(4) ? v(4) : v(nc) > v(3) ? v(3) : v(nc)
.ENDS
*---------------------------------------------------------------------
